// Current gradle version 6.5.

plugins {
    id 'net.ltgt.errorprone' apply false version "4.2.0"
    id "com.gradleup.shadow" version "8.3.6"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'
    apply plugin: "signing"

    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'com.gradleup.shadow'

    group = 'com.google.tsunami'
    version = '0.1.1-SNAPSHOT' // Current Tsunami version

    repositories {
        maven { // The google mirror is less flaky than mavenCentral()
            url 'https://maven-central.storage-download.googleapis.com/repos/central/data/'
        }

        mavenCentral()
        mavenLocal()
    }

    ext {
        // All dependency versions.
        autoValueVersion = '1.10.4'
        classGraphVersion = '4.8.65'
        errorproneVersion = '2.38.0'
        floggerVersion = '0.5.1'
        googleCloudStorageVersion = '1.103.1'
        googleHttpClientVersion = '1.44.1'
        grpcVersion = '1.60.0'
        gsonVersion = '2.8.6'
        guavaVersion = '28.2-jre'
        guiceVersion = '6.0.0'
        hiveVersion = '4.0.1'
        javaxAnnotationVersion = '1.3.2'
        javaxInjectVersion = '1'
        jaxbVersion = '2.3.1'
        jcommanderVersion = '1.48'
        jsoupVersion = '1.9.2'
        junitVersion = '4.13'
        libajpVersion = '1.0.0'
        mockitoVersion = '5.12.0'
        mysqlVersion='8.0.33'
        okhttpVersion = '3.12.0'
        postgresqlVersion = '42.6.0'
        protobufVersion = '3.25.2'
        protocVersion = protobufVersion
        snakeyamlVersion = '1.26'
        tcsVersion = '0.0.1'
        truthVersion = '1.4.0'

        // All Tsunami dependencies.
        deps = [
                autovalue_annotation_processor: "com.google.auto.value:auto-value:${autoValueVersion}",
                autovalue: "com.google.auto.value:auto-value-annotations:${autoValueVersion}",
                classgraph: "io.github.classgraph:classgraph:${classGraphVersion}",
                errorprone: "com.google.errorprone:error_prone_annotations:${errorproneVersion}",
                flogger_backend: "com.google.flogger:flogger-system-backend:${floggerVersion}",
                flogger_google_ext: "com.google.flogger:google-extensions:${floggerVersion}",
                flogger: "com.google.flogger:flogger:${floggerVersion}",
                google_cloud_storage: "com.google.cloud:google-cloud-storage:${googleCloudStorageVersion}",
                google_http_client: "com.google.http-client:google-http-client:${googleHttpClientVersion}",
                grpc_context: "io.grpc:grpc-context:${grpcVersion}",
                grpc_core: "io.grpc:grpc-core:${grpcVersion}",
                grpc_netty: "io.grpc:grpc-netty:${grpcVersion}",
                grpc_protobuf: "io.grpc:grpc-protobuf:${grpcVersion}",
                grpc_services: "io.grpc:grpc-services:${grpcVersion}",
                grpc_stub: "io.grpc:grpc-stub:${grpcVersion}",
                grpc_testing: "io.grpc:grpc-testing:${grpcVersion}",
                gson: "com.google.code.gson:gson:${gsonVersion}",
                guava: "com.google.guava:guava:${guavaVersion}",
                guice_assisted: "com.google.inject.extensions:guice-assistedinject:${guiceVersion}",
                guice: "com.google.inject:guice:${guiceVersion}",
                hive: "org.apache.hive:hive-jdbc:${hiveVersion}",
                javax_annotations: "javax.annotation:javax.annotation-api:${javaxAnnotationVersion}",
                javax_inject: "javax.inject:javax.inject:${javaxInjectVersion}",
                jaxb_runtime: "org.glassfish.jaxb:jaxb-runtime:${jaxbVersion}",
                jcommander: "com.beust:jcommander:${jcommanderVersion}",
                jsoup: "org.jsoup:jsoup:${jsoupVersion}",
                libajp: "com.doyensec:libajp:${libajpVersion}",
                mysql: "com.mysql:mysql-connector-j:${mysqlVersion}",
                okhttp: "com.squareup.okhttp3:okhttp:${okhttpVersion}",
                postgresql:"org.postgresql:postgresql:${postgresqlVersion}",
                protobuf_lite: "com.google.protobuf:protobuf-javalite:${protobufVersion}",
                protobuf_util: "com.google.protobuf:protobuf-java-util:${protobufVersion}",
                protobuf: "com.google.protobuf:protobuf-java:${protobufVersion}",
                snakeyaml: "org.yaml:snakeyaml:${snakeyamlVersion}",
                tcs_common: "com.google.tsunami:tcs-common:${tcsVersion}",
                tcs_proto: "com.google.tsunami:tcs-proto:${tcsVersion}",

                // Test dependencies.
                guava_testlib: "com.google.guava:guava-testlib:${guavaVersion}",
                junit: "junit:junit:${junitVersion}",
                mockito: "org.mockito:mockito-core:${mockitoVersion}",
                mock_web_server: "com.squareup.okhttp3:mockwebserver:${okhttpVersion}",
                truth: "com.google.truth:truth:${truthVersion}",
                truth8: "com.google.truth.extensions:truth-java8-extension:${truthVersion}",
                truth_protobuf: "com.google.truth.extensions:truth-proto-extension:${truthVersion}",
        ]
    }

    // Resolving conflicting NameResolverProvider from grpc-core.jar and
    // grpc-netty-shaded.jar, https://github.com/grpc/grpc-java/issues/10853
    shadowJar {
        zip64 true
        mergeServiceFiles()
    }

    if (rootProject.properties.get('errorProne', true)) {
        dependencies {
            errorprone "com.google.errorprone:error_prone_core:${errorproneVersion}"
            errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
        }

        // Disable ErrorProne for all generated codes.
        tasks.withType(JavaCompile).configureEach {
            options.errorprone.disableWarningsInGeneratedCode = false
            options.errorprone.excludedPaths = '.*/build/generated/.*'
        }
    } else {
        // Disable Error Prone
        allprojects {
            afterEvaluate { project ->
                project.tasks.withType(JavaCompile) {
                    options.errorprone.enabled = false
                }
            }
        }
    }

    plugins.withId('java') {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11

        java.withJavadocJar()
        java.withSourcesJar()

        jar.manifest {
            attributes('Implementation-Title': name,
                    'Implementation-Version': version,
                    'Built-By': System.getProperty('user.name'),
                    'Built-JDK': System.getProperty('java.version'),
                    'Source-Compatibility': sourceCompatibility,
                    'Target-Compatibility': targetCompatibility)
        }

        // Log stacktrace to console when test fails.
        test {
            testLogging {
                exceptionFormat = 'full'
                showExceptions true
                showCauses true
                showStackTraces true
            }
            maxHeapSize = '1500m'
        }
    }

    plugins.withId('maven-publish') {
        publishing {
            publications {
                maven(MavenPublication) {
                    from components.java

                    pom {
                        name = project.group + ':' + project.name
                        url = 'https://github.com/google/tsunami-security-scanner'
                        afterEvaluate {
                            // description is not available until evaluated.
                            description = project.description
                        }

                        licenses {
                            license {
                                name = 'Apache 2.0'
                                url = 'https://opensource.org/licenses/Apache-2.0'
                            }
                        }

                        scm {
                            connection = 'scm:git:https://github.com/google/tsunami-security-scanner.git'
                            developerConnection = 'scm:git:git@github.com:google/tsunami-security-scanner.git'
                            url = 'https://github.com/google/tsunami-security-scanner'
                        }

                        developers {
                            developer {
                                id = 'com.google.tsunami'
                                name = 'Tsunami Contributors'
                                email = 'tsunami-security-scanner@googlegroups.com'
                                url = 'https://github.com/google/tsunami-security-scanner'
                                organization = 'Tsunami Authors'
                                organizationUrl = 'https://www.google.com'
                            }
                        }
                    }
                }
            }

            repositories {
                maven {
                    def stagingUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    def releaseUrl = stagingUrl
                    def snapshotUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                    url = version.endsWith('SNAPSHOT') ? snapshotUrl : releaseUrl
                    credentials {
                        if (rootProject.hasProperty('ossrhUsername')
                                && rootProject.hasProperty('ossrhPassword')) {
                            username = rootProject.ossrhUsername
                            password = rootProject.ossrhPassword
                        }
                    }
                }

                mavenLocal()
            }
        }

        signing {
            sign publishing.publications.maven
        }

        shadowJar {
            archiveClassifier = null
        }
    }
}
